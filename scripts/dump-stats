#!/usr/bin/env escript
%% -*- erlang -*-

%% A script dumping all the stats archives in the passed directory in
%% json format to stdout.

-mode(compile).

fatal(Msg) ->
    fatal(Msg, []).
fatal(Fmt, Args) ->
    io:format(standard_error, "dump-stats: ~s~n",
              [io_lib:format(Fmt, Args)]),
    erlang:halt(1).

make_error(Error) ->
    {[{status, failed},
      {error, couch_util:to_binary(Error)}]}.

process_samples(Samples) ->
    [{[{ts, Ts},
       {sample, {Sample}}]} || {Ts, {stat_entry, _, Sample}} <- Samples].

do_read_stats_file(File) ->
    try
        Samples = pipes:run(pipes:read_file(File),
                            [pipes:gunzip(),
                             pipes:unmarshal_terms()],
                            pipes:collect()),
        {ok, Samples}
    catch
        T:E ->
            Stack = erlang:get_stacktrace(),
            {error, {T, E, Stack}}
    end.

read_stats_file(Path) ->
    RV = misc:with_file(Path, [raw, binary, read],
                        fun (File) ->
                                do_read_stats_file(File)
                        end),
    case RV of
        {ok, Samples} ->
            {[{status, ok},
              {samples, process_samples(Samples)}]};
        {error, Error} ->
            make_error(Error)
    end.

read_all_stats(StatsDir) ->
    RegExp = "^stats_archiver-(.+)\.(minute|hour|day|week|month|year).gz$",
    Stats0 =
        filelib:fold_files(
          StatsDir, RegExp, false,
          fun (Path, Acc) ->
                  FileName = filename:basename(Path),
                  {match, [Bucket, Period]} =
                      re:run(FileName, RegExp,
                             [anchored, {capture, all_but_first, list}]),

                  misc:dict_update(
                    Bucket,
                    fun (BucketAcc) ->
                            [{Period, read_stats_file(Path)} | BucketAcc]
                    end, [], Acc)
          end, dict:new()),

    Stats1 =
        dict:fold(fun (Bucket, Periods, Acc) ->
                          [{Bucket, {Periods}} | Acc]
                  end, [], Stats0),
    {Stats1}.

main([StatsDir, OutputFile]) ->
    Stats = read_all_stats(StatsDir),
    try
        ejson:encode(Stats)
    of Json ->
            ok = file:write_file(OutputFile, Json)
    catch T:E ->
            Stack = erlang:get_stacktrace(),
            fatal("failed to encode stats: ~p~n~p", [{T, E}, Stack])
    end;
main(_) ->
    fatal("usage: dump-stats <stats_dir> <output file>").
